apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion buildDetails.compileSdk

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "dev.arunkumar.android"
        minSdkVersion buildDetails.minSdk
        targetSdkVersion buildDetails.targetSdk

        versionCode 1
        versionName "1.0"

        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "dep"

    productFlavors {
        local {
            dimension "dep"
        }
        remote {
            dimension "dep"
        }
    }

    variantFilter { variant ->
        if (variant.name == "localRelease") {
            setIgnore(true)
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    localImplementation project(path: ':common-ext')
    remoteImplementation "dev.arunkumar.android:common-ext:1.0.0"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}"

    implementation "com.android.support:appcompat-v7:${versions.supportLibrary}"
    implementation "com.android.support.constraint:constraint-layout:${versions.constraintLayout}"

    testImplementation "junit:junit:${versions.junit}"
    testImplementation "org.robolectric:robolectric:${versions.roboelectric}"
}
